technique [
    name = "diffuse"
    ps_main="ps_main"
    vs_main="vs_main"
    class_name="DiffuseEffect"
           ];

struct DirLight {
  vec4 dir;
  vec4 diffuse;
  vec4 ambient;
};

[uniform_func="SetPVW"] uniform mat4 pvw;
[uniform_func="SetWorld"] uniform mat4 world;
[uniform_func="SetDirLight"] uniform DirLight light;
[uniform_func="SetGrassTex"] uniform Texture2D grass_tex;
[uniform_func="SetSlopeTex"] uniform Texture2D slope_tex;
[uniform_func="SetRockTex"] uniform Texture2D rock_tex;

exchange struct VsOutput {
  [semantic="position" system="true"]
  vec4 position;
  vec2 tex0;
  vec4 normal;
};

exchange struct VSInput {
  vec3 position;
  vec2 tex0;
  vec3 normal;
};

VsOutput vs_main(VSInput input) {
  VsOutput o;
  o.position = pvw * vec4(input.position, 1.0f);
  o.normal = world * vec4(input.normal, 0.0);
  o.tex0 = input.tex0;
  return o;
}

[system="true" semantic="target"]
vec4 ps_main(VsOutput o) {
  vec4 grass_color = sample2D(grass_tex, o.tex0);
  vec4 slope_color = sample2D(slope_tex, o.tex0);
  vec4 rock_color = sample2D(rock_tex, o.tex0);
  float slope = 1.0f - o.normal.y;
  float blend_amount;
  vec4 color;
  if (slope < 0.2) {
    blend_amount = slope / 0.2f;
    color = lerp(grass_color, slope_color, blend_amount);
  } else if (slope < 0.7f) {
    blend_amount = (slope - 0.2f) / (0.7f - 0.2f);
    color = lerp(grass_color, slope_color, blend_amount);
  } else {
    color = rock_color;
  }
  float diffuse_factor = clamp(dot(normalize(o.normal.xyz), -light.dir.xyz),
                               0.0, 1.0);
  vec3 light_factor = light.ambient.xyz + light.diffuse.xyz * diffuse_factor;
  return vec4(light_factor.xyz, 1.0) * color;
}

