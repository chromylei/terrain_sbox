technique [
    name = "water"
    ps_main="ps_main"
    vs_main="vs_main"
    class_name="WaterEffect"
           ];

struct DirLight {
  vec4 dir;
  vec4 diffuse;
  vec4 ambient;
};

[uniform_func="SetPVW"] uniform mat4 pvw;
[uniform_func="SetReflectPVW"] uniform mat4 reflect_pvw;
[uniform_func="SetWorld"] uniform mat4 world;
[uniform_func="SetDirLight"] uniform DirLight light;
[uniform_func="SetDiffuse"] uniform vec4 diffuse;
[uniform_func="SetTexture"] uniform Texture2D tex;
[uniform_func="SetRefTexture"] uniform Texture2D reftex;

exchange struct VsOutput {
  [semantic="position" system="true"]
  vec4 position;
  vec2 texcoord;
  vec4 normal;
  vec4 reflect_position;
};

exchange struct VSInput {
  vec3 position;
  vec2 texcoord;
  vec3 normal;
};

VsOutput vs_main(VSInput input) {
  VsOutput o;
  o.position = pvw * vec4(input.position, 1.0);
  o.normal = world * vec4(input.normal, 0.0);
  o.texcoord = input.texcoord;
  o.reflect_position = reflect_pvw * vec4(input.position, 1.0);
  return o;
}

[system="true" semantic="target"]
vec4 ps_main(VsOutput o) {
  vec2 reflect_texcoord;
  reflect_texcoord.x = o.reflect_position.x / o.reflect_position.w / 2.0f + 0.5f;
  reflect_texcoord.y = -o.reflect_position.y / o.reflect_position.w / 2.0f + 0.5f;

  vec4 tex_color = sample2D(tex, o.texcoord);
  vec4 reftex_color = sample2D(reftex, reflect_texcoord);
  float diffuse_factor = clamp(dot(normalize(o.normal.xyz), -light.dir.xyz),
                               0.0, 1.0);
  vec3 light_factor = light.ambient.xyz + light.diffuse.xyz * diffuse_factor;
  vec4 color = vec4(light_factor.xyz * diffuse.xyz, 1.0) * tex_color;
  return lerp(tex_color, reftex_color, 0.15f);
}

